---
title: Machine learning-funktioner i Azure Data Explorer
description: Använd maskininlärningsklustrning för root cause-analys i Azure Data Explorer.
author: orspod
ms.author: orspodek
ms.reviewer: adieldar
ms.service: data-explorer
ms.topic: conceptual
ms.date: 04/29/2019
ms.openlocfilehash: fe72031ef9ade7473dc4d5de7e090e92ef2a6843
ms.sourcegitcommit: 2ec4b3d0bad7dc0071400c2a2264399e4fe34897
ms.translationtype: MT
ms.contentlocale: sv-SE
ms.lasthandoff: 03/27/2020
ms.locfileid: "74769939"
---
# <a name="machine-learning-capability-in-azure-data-explorer"></a>Machine learning-funktioner i Azure Data Explorer

Azure Data Explorer, en Big Data Analytics-plattform, används för att övervaka tjänstens hälsa, QoS eller felaktiga enheter för avvikande beteende med hjälp av inbyggda [avvikelseidentifierings- och prognosfunktioner.](/azure/data-explorer/anomaly-detection) När ett avvikande mönster har identifierats utförs en rotsaksanalys (RCA) för att minska eller lösa anomalin.

Diagnosprocessen är komplex och lång och görs av domänexperter. Processen omfattar hämtning och sammanfogning av ytterligare data från olika källor under samma tidsperiod, letar efter förändringar i fördelningen av värden på flera dimensioner, kartläggning av ytterligare variabler och andra tekniker baserade på domänkunskap och Intuition. Eftersom dessa diagnosscenarier är vanliga i Azure Data Explorer, är machine learning-plugins tillgängliga för att göra diagnosfasen enklare och förkorta rca-varaktigheten.

Azure Data Explorer har tre [`autocluster`](/azure/kusto/query/autoclusterplugin)machine [`basket`](/azure/kusto/query/basketplugin)learning-plugins: , och [`diffpatterns`](/azure/kusto/query/diffpatternsplugin). Alla plugins implementerar klusteralgoritmer. Den `autocluster` `basket` och plugins kluster en `diffpatterns` enda post som och plugin kluster skillnaderna mellan två postuppsättningar.

## <a name="clustering-a-single-record-set"></a>Samla en enda postuppsättning

Ett vanligt scenario innehåller en datauppsättning som valts av ett visst villkor, till exempel tidsfönster som uppvisar avvikande beteende, enhetsavläsningar med hög temperatur, kommandon med lång varaktighet och användare med de bästa utgifterna. Vi vill ha ett enkelt och snabbt sätt att hitta gemensamma mönster (segment) i data. Mönster är en delmängd av datauppsättningen vars poster delar samma värden över flera dimensioner (kategoriska kolumner). Följande fråga bygger och visar en tidsserie med tjänstundantag över en vecka på tio minuters lagerplatser:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA5XPsaoCQQyF4d6nCFa7oHCtZd9B0F6G8ajByWTJZHS5+PDOgpVgYRn485EkOAnno9NAriWGFKw7QfQYUy0O43zZ0JNKFQnG/5jrbmeIXHBgwd6DjH2/JVqk2QrTL1aYvlifa4tni29YlzaiUK4yRK3Zu54006dBZ1N5/+X6PqpRI23+pFGGfIKRtz5egzk92K+dsycMyz3szhGEKWJ01lxI760O9ABuq0bMcvV2hqFoqnOz7F9BdSHlSgEAAA==)**\]**

```kusto
let min_t = toscalar(demo_clustering1 | summarize min(PreciseTimeStamp));  
let max_t = toscalar(demo_clustering1 | summarize max(PreciseTimeStamp));  
demo_clustering1
| make-series num=count() on PreciseTimeStamp from min_t to max_t step 10m
| render timechart with(title="Service exceptions over a week, 10 minutes resolution")
```

![Tidsschema för tjänstundantag](media/machine-learning-clustering/service-exceptions-timechart.png)

Antalet tjänstundantag korrelerar med den totala servicetrafiken. Du kan tydligt se det dagliga mönstret, för arbetsdagar måndag till fredag, med en ökning av tjänsten undantag räknas mitt på dagen, och droppar i räknas under natten. Platta låga punkter syns under helgen. Undantagsuppsättningar kan identifieras med hjälp av [avvikelseidentifiering av tidsserier](/azure/data-explorer/anomaly-detection?#time-series-anomaly-detection) i Azure Data Explorer.

Den andra spiken i uppgifterna sker på tisdag eftermiddag. Följande fråga används för att ytterligare diagnostisera den här spiken. Använd frågan för att rita om diagrammet runt toppen i högre upplösning (åtta timmar på en minuts lagerplatser) för att kontrollera om det är en skarp topp och visa dess kantlinjer.

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAAyXNwQrCMBAE0Hu/YvHUooWkghSl/yDoyUsJyWpCk2xJNnjx403pbeYwbzwyBBdnnoxiZBewHYS89GLshzNIeRWiuzUGA83al8yYXPzI5gdBLdjnWjFDLGHSVCK3HVCEe0LtMj4r9mAVVngnCvsLMO3hOFqo2goyVCxhNJhgu9dWJYavY9uyY4/T4UV1XVm2CEM0kFe34AnkBhXGOs7kCzuKh+4P3/XM5M8AAAA=)**\]**

```kusto
let min_t=datetime(2016-08-23 11:00);
demo_clustering1
| make-series num=count() on PreciseTimeStamp from min_t to min_t+8h step 1m
| render timechart with(title="Zoom on the 2nd spike, 1 minute resolution")
```

![Fokus på topptidsschema](media/machine-learning-clustering/focus-spike-timechart.png)

Vi ser en smal två minuters spik från 15:00 till 15:02. I följande fråga räknar du undantagen i det här tvåminutersfönstret:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA8tJLVHIzcyLL0hNzI4vsU1JLEktycxN1TAyMDTTNbDQNTJWMDS1MjDQtObKASlNrCCk1AioNCU1Nz8+Oae0uCS1KDMv3ZCrRqE8I7UoVSGgKDU5szg1BKgvuCQxt0AhKbWkPDU1TwPhBj09hCWaQI3J+aV5JQACnQoRpwAAAA==)**\]**

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| count
```

|Antal |
|---------|
|972    |

I följande fråga kan du ta prov på 20 undantag av 972:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA4XOsQrCMBSF4b1Pccd2aLmJKKL4DoLu4doeNDSJJb1SBx/eOHV0/37OCVCKPrkJMjo9DaJQH1FbNruW963dkNkemJtjFX5U3v+oLXRAfLo+vGZF9uluqg8tD2TQOaP3M66lu6jEiW7QBUj1+qHr1pGmhCojyPIX7QHvzakAAAA=)**\]**

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| take 20
```

| PreciseTimeStamp            | Region | SkalaUnit | DeploymentId (distributionId)                     | Tracepoint (spårpunkt) | Servicehost                          |
|-----------------------------|--------|-----------|----------------------------------|------------|--------------------------------------|
| 2016-08-23 15:00:08.7302460 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 100005     | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:09.9496584 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 8d257da1-7a1c-44f5-9acd-f9e02ff507fd |
| 2016-08-23 15:00:10.5911748 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 100005     | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:12.2957912 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007007   | f855fcef-ebfe-405d-aaf8-9c5e2e43d862 |
| 2016-08-23 15:00:18.5955357 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 9d390e07-417d-42eb-bebd-793965189a28 |
| 2016-08-23 15:00:20.7444854 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 6e54c1c8-42d3-4e4e-8b79-9bb076ca71f1 |
| 2016-08-23 15:00:23.8694999 | eus2 (på andra)   | su2 (på andra)       | 89e2f62a73bb4efd8f545aeae40d7e51 | 36109      | 19422243-19b9-4d85-9ca6-bc961861d287 |
| 2016-08-23 15:00:26.4271786 | ncus (ncus)   | su1 (på)       | e24ef436e02b4823ac5d5b1465a9401e | 36109      | 3271bae4-1c5b-4f73-98ef-cc117e9be914 |
| 2016-08-23 15:00:27.8958124 | scus (scus)   | su3 (på)       | 90d3d2fc7ecc430c9621ece335651a01 | 904498     | 8cf38575-fca9-48ca-bd7c-21196f6d6765 |
| 2016-08-23 15:00:32.9884969 | scus (scus)   | su3 (på)       | 90d3d2fc7ecc430c9621ece335651a01 | 10007007   | d5c7c825-9d46-4ab7-a0c1-8e2ac1d83ddb |
| 2016-08-23 15:00:34.5061623 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 1002110    | 55a71811-5ec4-497a-a058-140fb0d611ad |
| 2016-08-23 15:00:37.4490273 | scus (scus)   | su3 (på)       | 90d3d2fc7ecc430c9621ece335651a01 | 10007006   | f2ee8254-173c-477d-a1de-4902150ea50d |
| 2016-08-23 15:00:41.2431223 | scus (scus)   | su3 (på)       | 90d3d2fc7ecc430c9621ece335651a01 | 103200     | 8cf38575-fca9-48ca-bd7c-21196f6d6765 |
| 2016-08-23 15:00:47.2983975 | ncus (ncus)   | su1 (på)       | e24ef436e02b4823ac5d5b1465a9401e | 423690590  | 00000000-0000-0000-0000-000000000000 |
| 2016-08-23 15:00:50.5932834 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007006   | 2a41b552-aa19-4987-8cdd-410a3af016ac |
| 2016-08-23 15:00:50.8259021 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 1002110    | 0d56b8e3-470d-4213-91da-97405f8d005e |
| 2016-08-23 15:00:53.2490731 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 36109      | 55a71811-5ec4-497a-a058-140fb0d611ad |
| 2016-08-23 15:00:57.0000946 | eus2 (på andra)   | su2 (på andra)       | 89e2f62a73bb4efd8f545aeae40d7e51 | 64038      | cb55739e-4afe-46a3-970f-1b49d8ee7564 |
| 2016-08-23 15:00:58.2222707 | scus (scus)   | su5 (på)       | 9dbd1b161d5b4779a73cf19a7836ebd6 | 10007007   | 8215dcf6-2de0-42bd-9c90-181c70486c9c |
| 2016-08-23 15:00:59.9382620 | scus (scus)   | su3 (på)       | 90d3d2fc7ecc430c9621ece335651a01 | 10007006   | 451e3c4c-0808-4566-a64d-84d85cf30978 |

### <a name="use-autocluster-for-single-record-set-clustering"></a>Använd autocluster() för enstaka postuppsättningskluster

Även om det finns mindre än tusen undantag är det fortfarande svårt att hitta gemensamma segment, eftersom det finns flera värden i varje kolumn. Du kan [`autocluster()`](/azure/kusto/query/autoclusterplugin) använda plugin för att omedelbart extrahera en liten lista över vanliga segment och hitta intressanta kluster inom spiken två minuter som ses i följande fråga:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA4WOsQrCMBRF937FG5OhJYkoovQfBN1DbC8aTNqSvlgHP94IQkf3c+65AUzRD3aCe1hue8dgHyGM0rta7WuzIb09KCWPVfii7vUPNQXtEUfbhTwzkh9uunrTckcCnRI6P+NSvDO7ONEVvACDWD80zRqRRcTThVxa5DKPv00hP81KL1+4AAAA)**\]**

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| evaluate autocluster()
```

| SegmentId (olikartade) | Antal | Procent | Region | SkalaUnit | DeploymentId (distributionId) | Servicehost |
|-----------|-------|------------------|--------|-----------|----------------------------------|--------------------------------------|
| 0 | 639 | 65.7407407407407 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 1 | 94 | 9.67078189300411 | scus (scus) | su5 (på) | 9dbd1b161d5b4779a73cf19a7836ebd6 |  |
| 2 | 82 | 8.43621399176955 | ncus (ncus) | su1 (på) | e24ef436e02b4823ac5d5b1465a9401e |  |
| 3 | 68 | 6.99588477366255 | scus (scus) | su3 (på) | 90d3d2fc7ecc430c9621ece335651a01 |  |
| 4 | 55 | 5.65843621399177 | Veu | su4 (på andra) | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |

Av resultaten ovan framgår att det mest dominerande segmentet innehåller 65,74 % av de totala undantagsposterna och delar fyra dimensioner. Nästa segment är mycket mindre vanligt, innehåller endast 9,67% av posterna och delar tre dimensioner. De andra segmenten är ännu mindre vanliga. 

Autocluster använder en egen algoritm för gruvdrift flera dimensioner och extrahera intressanta segment. "Intressant" innebär att varje segment har betydande täckning av både de poster som och de funktioner som. Segmenten är också avvikit, vilket innebär att var och en skiljer sig avsevärt från de andra. Ett eller flera av dessa segment kan vara relevanta för RCA-processen. För att minimera granskning och bedömning av segment extraherar autokluster endast en liten segmentlista.

### <a name="use-basket-for-single-record-set-clustering"></a>Använd basket() för enstaka postuppsättningskluster

Du kan också [`basket()`](/azure/kusto/query/basketplugin) använda plugin som visas i följande fråga:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA4WOsQ6CMBgGd57iH9sB0tZojMZ3MNG9KfBFG1og7Y84+PDWidH9LncBTNGPdoYbLF96x2AfIYzSh1oda7MjvT8pJc9V+KHu/Q81Be0RJ9uFJTOSHx+6+tD6RAJdEzqfcS/ejV2cqQWvwCi2h6bZIrKIeLmwlBa1Lg9gIb9KJv2TswAAAA==)**\]**

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
demo_clustering1
| where PreciseTimeStamp between(min_peak_t..max_peak_t)
| evaluate basket()
```

| SegmentId (olikartade) | Antal | Procent | Region | SkalaUnit | DeploymentId (distributionId) | Tracepoint (spårpunkt) | Servicehost |
|-----------|-------|------------------|--------|-----------|----------------------------------|------------|--------------------------------------|
| 0 | 639 | 65.7407407407407 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 |  | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 1 | 642 | 66.0493827160494 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 |  |  |
| 2 | 324 | 33.3333333333333 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 | 0 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 3 | 315 | 32.4074074074074 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 | 16108 | e7f60c5d-4944-42b3-922a-92e98a8e7dec |
| 4 | 328 | 33.7448559670782 |  |  |  | 0 |  |
| 5 | 94 | 9.67078189300411 | scus (scus) | su5 (på) | 9dbd1b161d5b4779a73cf19a7836ebd6 |  |  |
| 6 | 82 | 8.43621399176955 | ncus (ncus) | su1 (på) | e24ef436e02b4823ac5d5b1465a9401e |  |  |
| 7 | 68 | 6.99588477366255 | scus (scus) | su3 (på) | 90d3d2fc7ecc430c9621ece335651a01 |  |  |
| 8 | 167 | 17.1810699588477 | scus (scus) |  |  |  |  |
| 9 | 55 | 5.65843621399177 | Veu | su4 (på andra) | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |  |
| 10 | 92 | 9.46502057613169 |  |  |  | 10007007 |  |
| 11 | 90 | 9.25925925925926 |  |  |  | 10007006 |  |
| 12 | 57 | 5.8641975308642 |  |  |  |  | 00000000-0000-0000-0000-000000000000 |

Korg implementerar Apriori-algoritmen för artikeluppsättningsbrytning och extraherar alla segment vars täckning av posten är över ett tröskelvärde (standard 5%). Du kan se att fler segment extraherades med liknande (till exempel segmenten 0,1 eller 2,3).

Båda plugins är kraftfulla och lätta att använda, men deras betydande begränsning är att de kluster en enda post som på ett oövervakat sätt (utan etiketter). Det är därför oklart om de extraherade mönstren kännetecknar den valda postuppsättningen (de avvikande posterna) eller den globala postuppsättningen.

## <a name="clustering-the-difference-between-two-records-sets"></a>Samla skillnaden mellan två posteruppsättningar

Insticksprogrammet [`diffpatterns()`](/azure/kusto/query/diffpatternsplugin) övervinner `autocluster` `basket`begränsningen av och . `Diffpatterns`tar två postuppsättningar och extraherar de huvudsegment som skiljer sig mellan dem. En uppsättning innehåller vanligtvis den avvikande postuppsättning som `autocluster` undersöks (en analyseras av och `basket`). Den andra uppsättningen innehåller referenspostuppsättningen (baslinjen). 

I frågan nedan använder `diffpatterns` vi för att hitta intressanta kluster inom spikens två minuter, som skiljer sig från kluster inom baslinjen. Vi definierar baslinjefönstret som åtta minuter före 15:00 (när spiken startade). Vi måste också utöka med en binär kolumn (AB) som anger om en specifik post tillhör baslinjen eller den avvikande uppsättningen. `Diffpatterns`implementerar en övervakad inlärningsalgoritm, där de två klassetiketterna genererades av den avvikande kontra baslinjeflaggan (AB).

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA42QzU+DQBDF7/wVcwOi5UtrmhJM4OzBRO9kWqbtpssuYacfGv94t0CrxFTd02by5jfvPUkMtVBlQ7gtOauQiUVNXhLFD5NoNknuIJ7Oo8hPHXmS4vEvaXKWWuoCDUmh6Jr8fj79Tv6HfOanEIbwRLgnQFhjAwviA5EC3hCcCYCq6gamEVsC1oB7LfoRt6iMYKEVvGtFQXfeNFKc7mXe2MjNVzl+mARR6lRU63Ipd4apFWodOx9w2FBL4D23tBSGXi3mhbG+OPPGVQTB+ITvg24dGN7vlN5JTxhc+dYAHZls4LzIxGr1k/B4iXcLbq50jfLNtd9i8OB2jD3KnW0dKstokG08Zby8uLbyCfX/tG46AgAA)**\]**

```kusto
let min_peak_t=datetime(2016-08-23 15:00);
let max_peak_t=datetime(2016-08-23 15:02);
let min_baseline_t=datetime(2016-08-23 14:50);
let max_baseline_t=datetime(2016-08-23 14:58); // Leave a gap between the baseline and the spike to avoid the transition zone.
let splitime=(max_baseline_t+min_peak_t)/2.0;
demo_clustering1
| where (PreciseTimeStamp between(min_baseline_t..max_baseline_t)) or
        (PreciseTimeStamp between(min_peak_t..max_peak_t))
| extend AB=iff(PreciseTimeStamp > splitime, 'Anomaly', 'Baseline')
| evaluate diffpatterns(AB, 'Anomaly', 'Baseline')
```

| SegmentId (olikartade) | CountA | CountB (antal) | ProcentA | ProcentB | ProcentDiffAB | Region | SkalaUnit | DeploymentId (distributionId) | Tracepoint (spårpunkt) |
|-----------|--------|--------|----------|----------|---------------|--------|-----------|----------------------------------|------------|
| 0 | 639 | 21 | 65.74 | 1.7 | 64.04 | Eau | su7 (på andra) | b5d1d4df547d4a04ac15885617edba57 |  |
| 1 | 167 | 544 | 17.18 | 44.16 | 26.97 | scus (scus) |  |  |  |
| 2 | 92 | 356 | 9.47 | 28.9 | 19.43 |  |  |  | 10007007 |
| 3 | 90 | 336 | 9.26 | 27.27 | 18.01 |  |  |  | 10007006 |
| 4 | 82 | 318 | 8.44 | 25.81 | 17.38 | ncus (ncus) | su1 (på) | e24ef436e02b4823ac5d5b1465a9401e |  |
| 5 | 55 | 252 | 5.66 | 20.45 | 14.8 | Veu | su4 (på andra) | be1d6d7ac9574cbc9a22cb8ee20f16fc |  |
| 6 | 57 | 204 | 5.86 | 16.56 | 10.69 |  |  |  |  |

Det mest dominerande segmentet är samma `autocluster`segment som extraherades av , dess täckning på två minuters avvikande fönster är också 65,74%. Men täckningen på baslinjefönstret på åtta minuter är bara 1,7 %. Skillnaden är 64,04%. Den här skillnaden verkar vara relaterad till den avvikande spiken. Du kan verifiera det här antagandet genom att dela upp det ursprungliga diagrammet i de poster som tillhör det här problematiska segmentet jämfört med de andra segmenten som visas i frågan nedan:

**\[**[**Klicka för att köra fråga**](https://dataexplorer.azure.com/clusters/help/databases/Samples?query=H4sIAAAAAAAAA5WRsWrDMBCG9zzF4cmGGuJUjh2Ktw7tUkLTzuEsnRNRnRQkuSQlD185yRTo0EWIO913/J8MRWBttxE6iC5INOhzRey20owhktd2V8EZwsiMXv/Q9Dpfe5I60Idm2kTkQ1E8AczMxMLjf1h4/IN1PzY7Ax0jWQWBdomvhyF/p512FroOMsIxA0zdTdpKn1bHSzmMzbX8TAfjTkw2vqpLp69VpYQaatEogXOBsqrbtl5WDake6yabXWjkv7WkFxeuPGqG5VzWqhQrIUqx6B/L1WKB6aBViy01imT2ANnau94QT9c35xlNVqQAjF9UhpSHAtiRO+lGG/MCUoZ7CTB4x7ePie5mNbk4QDVn6E+ThUT0SQh5iGlM7tHHX4WFgLHOAQAA)**\]**

```kusto
let min_t = toscalar(demo_clustering1 | summarize min(PreciseTimeStamp));  
let max_t = toscalar(demo_clustering1 | summarize max(PreciseTimeStamp));  
demo_clustering1
| extend seg = iff(Region == "eau" and ScaleUnit == "su7" and DeploymentId == "b5d1d4df547d4a04ac15885617edba57"
and ServiceHost == "e7f60c5d-4944-42b3-922a-92e98a8e7dec", "Problem", "Normal")
| make-series num=count() on PreciseTimeStamp from min_t to max_t step 10m by seg
| render timechart
```

![Validera tidsdiagram för diffpattern](media/machine-learning-clustering/validating-diffpattern-timechart.png)

Detta diagram tillåter oss att se att spiken på tisdag eftermiddag var `diffpatterns` på grund av undantag från detta specifika segment, upptäcktes med hjälp av plugin.

## <a name="summary"></a>Sammanfattning

Azure Data Explorer Machine Learning plugins är användbara för många scenarier. Den `autocluster` `basket` och genomföra oövervakad inlärningsalgoritm och är lätta att använda. `Diffpatterns`implementerar övervakad inlärningsalgoritm och även mer komplex, det är mer kraftfull i att extrahera differentiering segment för RCA.

Dessa plugins används interaktivt i ad hoc-scenarier och i automatiska övervakningstjänster i nära realtid. I Azure Data Explorer följs avvikelseidentifiering av tidsserier av en diagnosprocess som är mycket optimerad för att uppfylla nödvändiga prestandastandarder.
